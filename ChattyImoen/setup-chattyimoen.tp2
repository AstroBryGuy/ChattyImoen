/*
 * "Chatty Imoen" for BGEE
 * AstroBryGuy
 *
*/

/* Backup folder */
BACKUP ~chattyimoen/backup~

AUTHOR ~AstroBryGuy~

/* enable all error messages; nothing suppressed. comment this out for release version */
MODDER

VERSION ~v1.0-20160406~

README ~chattyimoen/readme.md~

ALWAYS
	INCLUDE ~chattyimoen/lib/project_macros.tpa~   /* adds macros callable by each component */

	/* STATE.IDS patching - thanks, Cam, if you read it */
	/* adds custom IsValidForPartyDialogue state */
	APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~
END

AUTO_TRA ~chattyimoen/tra/%s~
LANGUAGE ~English~ ~English~ ~chattyimoen/tra/english/setup-chattyimoen.tra~

BEGIN @0 DESIGNATED 0 /* Chatty Imoen: Expanded Character Sounds */
  REQUIRE_PREDICATE GAME_IS ~bgee~ @1 /* You must have Baldur's Gate: Enhanced Edition installed. */

COPY ~chattyimoen/sounds/%LANGUAGE%~ ~override~

ACTION_FOR_EACH imoen IN imoen imoen1 imoen2 imoen4 imoen6 BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME "%imoen%.cre" BEGIN
		COPY_EXISTING ~%imoen%.cre~ ~override~
			LAUNCH_PATCH_MACRO ~source_cre_sound_cleanup~
			SAY INITIAL_MEETING @10 /* [IMOEN 15] Heya! It's me, Imoen. */
			SAY MORALE @11 /* [IMOEN 7] I am gone. */
			SAY HAPPY @12 /* [IMOEN 27] Good on you if you save the day. */
			SAY UNHAPPY_ANNOYED @13 /* [IMOEN 10] Mutton-mongerin' riffraff. */
			SAY UNHAPPY_SERIOUS @14 /* [IMOEN 17] I've done had enough of this. */
			SAY UNHAPPY_BREAKING @15 /* [IMOEN 21] Gettin' out alive, I am. 'tis nothin' for me here now. */
			SAY LEADER @16 /* [AY#IMNLD] Aww, come on. I'm not cut out for the leadership stuff. */
			SAY TIRED @17 /* [IMOEN 11] *yawn* I'm gettin' a little sleepy. */
			SAY BORED @18 /* [AY#IMNBR] What's taking you so long? */
			SAY BATTLE_CRY1 @19 /* [IMOEN 18] My blade will cut you down to size! */
			SAY BATTLE_CRY2 @20 /* [AY#IMNB2] I'll show you a fight! */
			SAY DAMAGE @21 /* [IMOEN 12]  */
			SAY DYING @22 /* [IMOEN 13]  */
			SAY HURT @23 /* [AY#IMNHR] I'm kinda wounded. */
			SAY AREA_DUNGEON @24 /* [AY#IMNDN] There could be traps down here. */
			SAY AREA_NIGHT @25 /* [IMOEN 19] 'Tis something most unnatural here, and I want no part of it. */
			SAY SELECT_COMMON1 @26 /* [IMOEN 3] Yep? */
			SAY SELECT_COMMON2 @27 /* [IMOEN 4] Whatcha want? */
			SAY SELECT_COMMON3 @28 /* [AY#IMNS3] Name it. */
			SAY SELECT_COMMON4 @29 /* [AY#IMNS4] Good to go. */
			SAY SELECT_COMMON5 @30 /* [AY#IMNS5] Somethin' up? */
			SAY SELECT_COMMON6 @31 /* [AY#IMNS6] Hurry up! */
			SAY SELECT_ACTION1 @34 /* [AY#IMNA1] Gotcha. */
			SAY SELECT_ACTION2 @35 /* [AY#IMNA2] You can count on me! */
			SAY SELECT_ACTION3 @36 /* [AY#IMNA3] No problem at all. */
			SAY SELECT_ACTION4 @37 /* [IMOEN 5] Booooorrrring. */
			SAY SELECT_ACTION5 @38 /* [IMOEN 6] Ha, ye're a queer fellow. */
			SAY SELECT_ACTION6 @39 /* [AY#IMNA6] Alright, alright! */
			SAY SELECT_ACTION7 @40 /* [IMOEN 28] I know nothin' more, so leave me to go. */
			SAY INSULT @13 /* [IMOEN 10] Mutton-mongerin' riffraff. */
			SAY COMPLIMENT1 @41 /* [AY#IMNC1] Good job! */
			SAY SPECIAL1 @42 /* [AY#IMNSP] Heya! How're things goin'? */
			SAY SPECIAL2 @43 /* [IMOEN 8] Do ya wanna tell me a story 'bout trollops an' plug tails? Please? */
			SAY SPECIAL3 @44 /* [IMOEN 30] Back home, Puffguts would always tell me a story. */
			SAY RESPONSE_TO_INSULT1 @45 /* [IMOEN 26] I care not. */
			SAY RESPONSE_TO_INSULT2 @46 /* [IMOEN 9] Ye're all buffle-headed. */
			SAY MISCELLANEOUS @47 /* [AY#IMNR1] Alright! */
			SAY RESPONSE_TO_COMPLIMENT2 @48 /* [AY#IMNR2] Right you are. */
			SAY REACT_TO_DIE_GENERAL @49 /* [IMOEN 14] Poor sod, takin' the dirt nap so soon. */
			SAY REACT_TO_DIE_SPECIFIC @49 /* [IMOEN 14] Poor sod, takin' the dirt nap so soon. */
			SAY HIDDEN_IN_SHADOWS @50 /* [IMOEN 24] This way. */
			SAY PICKED_POCKET @51 /* [AY#IMNPP] Easy as pie. */
		BUT_ONLY
	END
END

// Imoen greets party with "Good to see you Rum-dukes again!" line after being kicked out.
COMPILE EVALUATE_BUFFER ~chattyimoen/dlg/ay#imoenp.d~


BEGIN @2 /* Chatty Imoen:  Add NPC Interactions */
	REQUIRE_PREDICATE GAME_IS ~bgee~ @1 /* You must have Baldur's Gate: Enhanced Edition installed. */
	REQUIRE_COMPONENT ~setup-chattyimoen.tp2~ ~0~ @3 // ~Chatty Imoen: Expanded Character Sounds component required~

// Imoen gets specific death reaction for Garrick (no one else does)
COPY_EXISTING ~death.2da~ ~override/death.2da~
	COUNT_2DA_COLS "cnt_col"
    COUNT_2DA_ROWS %cnt_col% "cnt_row"
	FOR ( cnt=0; cnt<"%cnt_row%"; cnt=cnt+1 ) BEGIN
    	READ_2DA_ENTRY "%cnt%" 0 "%cnt_col%" "npc_name"
       	PATCH_IF ("%npc_name%" STR_EQ "IMOEN") BEGIN
       		SET_2DA_ENTRY "%cnt%" 9 "%cnt_col%" "1" // Garrick
       	END
    END
BUT_ONLY

COPY_EXISTING ~death.2da~ ~override/death.2da~
	LPM remove_blank_lines
BUT_ONLY

// Add Imoen to INTERACT.2DA
COPY_EXISTING ~interact.2da~ ~override~
	REPLACE_TEXTUALLY ~IMOEN~ ~AY#1234~
BUT_ONLY

COPY_EXISTING ~interact.2da~ ~override~
	COUNT_2DA_COLS "cnt_col"
	COUNT_2DA_ROWS %cnt_col% "cnt_row"
	SPRINT new_row ~IMOEN s 0 s 0 c s s c s s 0 i 0 s s 0 i i i i s 0 i 0~      
	FOR ( cnt=25; cnt<"%cnt_col%"; cnt=cnt+1 ) BEGIN
    	SPRINT new_row ~%new_row% 0~
	END
	SPRINT new_col "$ $ IMOEN "
	SET cnt_row = %cnt_row% + 1
	FOR ( cnt=0; cnt<"%cnt_row%"; cnt=cnt+1 ) BEGIN
    	SPRINT new_col ~%new_col% 0~
    END    
BUT_ONLY

APPEND "interact.2da" "%new_row%" UNLESS ~IMOEN~    
APPEND_COL "interact.2da" "%new_col%"

COPY_EXISTING ~interact.2da~ ~override~
	COUNT_2DA_COLS "cnt_col"
    COUNT_2DA_ROWS %cnt_col% "cnt_row"
	SET imoen_col = %cnt_col% - 1
	FOR ( cnt=0; cnt<"%cnt_row%"; cnt=cnt+1 ) BEGIN
    	READ_2DA_ENTRY "%cnt%" 0 "%cnt_col%" "npc_name"
       	PATCH_IF ("%npc_name%" STR_EQ "KIVAN") BEGIN
    		SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END
        PATCH_IF ("%npc_name%" STR_EQ "ALORA") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END
        PATCH_IF ("%npc_name%" STR_EQ "MINSC") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "c"
        END
        PATCH_IF ("%npc_name%" STR_EQ "DYNAHEIR") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "c"
        END
        PATCH_IF ("%npc_name%" STR_EQ "YESLICK") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "c"
        END
        PATCH_IF ("%npc_name%" STR_EQ "CORAN") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "c"
        END
        PATCH_IF ("%npc_name%" STR_EQ "AJANTIS") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "s"
    	END
    	PATCH_IF ("%npc_name%" STR_EQ "KHALID") BEGIN
			SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "c"
        END
    	PATCH_IF ("%npc_name%" STR_EQ "JAHEIRA") BEGIN
    		SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
		END
        PATCH_IF ("%npc_name%" STR_EQ "GARRICK") BEGIN
    		SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "s"
    	END
		PATCH_IF ("%npc_name%" STR_EQ "SAFANA") BEGIN
			SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END
        PATCH_IF ("%npc_name%" STR_EQ "FALDORN") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "i"
        END
        PATCH_IF ("%npc_name%" STR_EQ "BRANWEN") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END
        PATCH_IF ("%npc_name%" STR_EQ "QUAYLE") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END
        PATCH_IF ("%npc_name%" STR_EQ "XAN") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END
        PATCH_IF ("%npc_name%" STR_EQ "SKIE") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END
        PATCH_IF ("%npc_name%" STR_EQ "ELDOTH") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END
        PATCH_IF ("%npc_name%" STR_EQ "XZAR") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "s"
        END
        PATCH_IF ("%npc_name%" STR_EQ "MONTARON") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "i"
        END
        PATCH_IF ("%npc_name%" STR_EQ "TIAX") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "s"
        END
        PATCH_IF ("%npc_name%" STR_EQ "KAGAIN") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "i"
    	END
        PATCH_IF ("%npc_name%" STR_EQ "SHARTEEL") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
    	END
        PATCH_IF ("%npc_name%" STR_EQ "EDWIN") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "i"
        END
        PATCH_IF ("%npc_name%" STR_EQ "VICONIA") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END 	
	END
BUT_ONLY

COPY_EXISTING ~interact.2da~ ~override~
	PRETTY_PRINT_2DA
BUT_ONLY

COPY_EXISTING ~interact.2da~ ~override~
	LPM remove_blank_lines
BUT_ONLY


BEGIN @5 /* Give Imoen her BG1 Portrait */
  SUBCOMPONENT @4  /* Imoen Portrait Replacements */
  REQUIRE_PREDICATE GAME_IS ~bgee~ @1 /* You must have Baldur's Gate: Enhanced Edition installed. */

ACTION_FOR_EACH imoen IN imoen imoen1 imoen2 imoen4 imoen6 bdimoen bdimoedo BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME "%imoen%.cre" BEGIN
		COPY_EXISTING ~%imoen%.CRE~ ~override~
      		PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN // protects against invalid files
        		WRITE_ASCII 0x34 ~imoenm~ #8 // small portrait
        		WRITE_ASCII 0x3c ~imoenl~ #8 // large portrait
        	END
        BUT_ONLY
    END
END

BEGIN @6 /* Give Imoen her SoD Portrait */
  SUBCOMPONENT @4  /* Imoen Portrait Replacements */
  REQUIRE_PREDICATE GAME_IS ~bgee~ @1 /* You must have Baldur's Gate: Enhanced Edition installed. */

ACTION_IF ( NOT FILE_EXISTS_IN_GAME ~bdimoenl.bmp~) THEN BEGIN
	COPY ~chattyimoen/bmp~ ~override~
END

ACTION_FOR_EACH imoen IN imoen imoen1 imoen2 imoen4 imoen6 bdimoen bdimoedo BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME "%imoen%.cre" BEGIN
		COPY_EXISTING ~%imoen%.CRE~ ~override~
      		PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN // protects against invalid files
        		WRITE_ASCII 0x34 ~bdimoenm~ #8 // small portrait
        		WRITE_ASCII 0x3c ~bdimoenl~ #8 // large portrait
        	END
        BUT_ONLY
    END
END

BEGIN @7 /* Give Imoen her BG2 Portrait */
  SUBCOMPONENT @4  /* Imoen Portrait Replacements */
  REQUIRE_PREDICATE GAME_IS ~bgee~ @1 /* You must have Baldur's Gate: Enhanced Edition installed. */

ACTION_FOR_EACH imoen IN imoen imoen1 imoen2 imoen4 imoen6 bdimoen bdimoedo BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME "%imoen%.cre" BEGIN
		COPY_EXISTING ~%imoen%.CRE~ ~override~
      		PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN // protects against invalid files
        		WRITE_ASCII 0x34 ~nimoenm~ #8 // small portrait
        		WRITE_ASCII 0x3c ~nimoenl~ #8 // large portrait
        	END
        BUT_ONLY
    END
END

